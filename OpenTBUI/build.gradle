plugins {
    alias(libs.plugins.android.library)
//    id 'com.android.library'
    id 'maven-publish'
}

android {
    def localProperties = new Properties()
    def localPropertiesFile = rootProject.file('local.properties')
    if (localPropertiesFile.exists()) {
        localProperties.load(new FileInputStream(localPropertiesFile))
    }

    signingConfigs {
        debug {
            storeFile file(localProperties.getProperty('SIGNING_KEY_PATH_OVERRIDE'))
            storePassword localProperties.getProperty('SIGNING_KEY_PASSWORD')
            keyPassword localProperties.getProperty('SIGNING_KEY_ALIAS_PASSWORD')
            keyAlias localProperties.getProperty('SIGNING_KEY_ALIAS')
        }
    }
    namespace 'dev1503.opentbui'
    compileSdk 36

    defaultConfig {
        minSdk 21

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation('androidx.appcompat:appcompat:1.7.1')
    implementation('androidx.core:core:1.17.0')
    implementation('androidx.recyclerview:recyclerview:1.4.0')
    implementation(project(":indicatorseekbar"))
    implementation("com.google.android.material:material:1.7.0")
    // https://mvnrepository.com/artifact/com.google.android.flexbox/flexbox
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
}
publishing {
    publications {
        release(MavenPublication) {
            groupId = 'dev1503.opentbui'
            artifactId = 'OpenTBUI'
            version = '202511151732.6'

            afterEvaluate { project ->
                println "Available components: ${components.names}"
                if (components.findByName('release')) {
                    from components.release
                } else if (components.findByName('debug')) {
                    from components.debug
                } else if (components.findByName('default')) {
                    from components.default
                } else {
                    // 如果没有找到标准组件，列出所有可用组件
                    components.all { component ->
                        println "Component: ${component.name}"
                    }
                }
            }
        }
    }
}